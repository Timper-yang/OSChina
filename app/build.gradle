apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'

def ext = rootProject.ext

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion ext.compileSdkVersion
    buildToolsVersion ext.buildToolsVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId ext.applicationId
        minSdkVersion ext.minSdkVersion
        targetSdkVersion ext.targetSdkVersion
        versionCode ext.versionCode
        versionName ext.versionName
        multiDexEnabled true
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    dataBinding {
        enabled = true
    }

    lintOptions {
        quiet true
        abortOnError false
        ignoreWarnings true
    }

    dexOptions {
        incremental true
        jumboMode true
        javaMaxHeapSize "4g"
    }


    repositories {
        flatDir {
            dirs 'libs'
        }
    }

    signingConfigs {
        config {
            storeFile file('../key/op.jks')
            keyAlias '苏州'
            keyPassword 'op2017'
            storePassword 'op2017'
        }
    }

    buildTypes {

        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
        }

        release {
            // 混淆
            minifyEnabled true
            // Zipalign优化
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources true
            // 前一部分代表系统默认的android程序的混淆文件，该文件已经包含了基本的混淆声明，后一个文件是自己的定义混淆文件
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        // 输出apk名称为appname-1.0-beta1-xxxx-xx-xx.apk
                        def fileName = "app-${defaultConfig.versionName}-${releaseTime()}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
    }
}

dependencies {

    def compiles = ext.compiles
    def provides = ext.provides
    def apts = ext.apts
    def release = ext.release

    compile project(':lib')
    compile project(':data')
    compile project(':third_party')
    compile fileTree(dir: 'libs', include: ['*.jar'])

    apt provides.daggerCompiler
    provided provides.annotation

    apt apts.logansquareCompiler


    debugCompile compiles.leakcanary
    releaseCompile release.leakcanary
    testCompile release.leakcanary
}